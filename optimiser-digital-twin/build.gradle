plugins {
    // Apply the application plugin to add support for building a CLI application in Java.
    id 'application'
    // This plugin creates a self-contained jar file including all
    // dependencies.  Staying at version 8.3.9 for now until we can diagnose
    // the build failure with versions >=9.0.0
    id 'com.gradleup.shadow' version '8.3.9'
    // Use this to check for newer versions of dependency libraries via
    // ./gradlew dependencyUpdates
    id "com.github.ben-manes.versions" version "0.53.0"
    // add jshell support (https://github.com/mrsarm/jshell-plugin):
    // rlwrap ./gradlew --console plain jshell
    id "com.github.mrsarm.jshell.plugin" version "1.2.1"
    // https://docs.freefair.io/gradle-plugins/8.4/reference/#_lombok
    id "io.freefair.lombok" version "9.1.0"
    // Source in ../buildSrc
    id "abs.compile-abs"
}

repositories {
    mavenCentral()

    // NebulOuS-developed components
    maven {
        url "https://s01.oss.sonatype.org/content/repositories/snapshots/"
        content {
            includeModule("eu.nebulouscloud", "exn-connector-java")
        }
    }
}

dependencies {
    // JSON parsing, also used by the data classes defined in sal-common:
    // https://github.com/FasterXML/jackson
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.20.1'
    // YAML, CSV parsing: https://github.com/FasterXML/jackson-dataformats-text
    implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.20.1'
    implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-csv:2.20.1'

    // Command-line parsing: https://picocli.info
    implementation ('info.picocli:picocli:4.7.7')

    // Logging: SLF4J (implemented by Logback)
    implementation 'ch.qos.logback:logback-classic:1.5.24'
    // Include logstash-logback-encoder for JSON formatting
    implementation 'net.logstash.logback:logstash-logback-encoder:9.0'

    // SQLite library
    implementation 'org.xerial:sqlite-jdbc:3.51.1.0'

    // the EXN Middleware:
    // https://openproject.nebulouscloud.eu/projects/nebulous-collaboration-hub/wiki/asynchronous-messaging-specification
    implementation('eu.nebulouscloud:exn-connector-java:1.0-SNAPSHOT') {
        exclude group: 'org.slf4j', module: 'slf4j-simple'
    }

    // Use JUnit Jupiter for testing.
    testImplementation 'org.junit.jupiter:junit-jupiter:6.0.2'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher:6.0.2'
}

// Apply a specific Java toolchain to ease working on different environments.
java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(25)
    }
}

application {
    // Define the main class for the application.
    mainClass = 'eu.nebulouscloud.optimiser.twin.Main'
}

jar {
    archiveBaseName='optimiser-digital-twin'
    manifest {
        attributes 'Implementation-Title': 'optimiser-digital-twin',
            'Bundle-Version': '0.1'
    }
}

shadowJar {
    // Creates a fat jar, including all dependencies
}

task distJar(type: Copy, dependsOn: shadowJar) {
    description = 'Copies the fat jar into its documented location.'
    group = 'build'
    from shadowJar.archiveFile
    into 'dist/'
}
clean {
    delete 'dist/'
}
assemble.dependsOn distJar


tasks.named('test') {
    // Use JUnit Platform for unit tests.
    useJUnitPlatform()
}
